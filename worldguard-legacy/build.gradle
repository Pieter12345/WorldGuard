apply plugin: 'eclipse'
apply plugin: 'idea'

repositories {
    maven { url "https://jitpack.io" }
}

dependencies {
    compile project(':worldguard-core')
    compile 'org.bukkit:bukkit:1.13-R0.1-SNAPSHOT'
    compile 'com.sk89q.worldedit:worldedit-bukkit:7.0.0-SNAPSHOT'
    compile ('com.sk89q:commandbook:2.3') {
        exclude group: 'com.sk89q', module: 'worldedit'
        exclude group: 'com.zachsthings.libcomponents'
    }
    compile 'org.bstats.bStats-Metrics:bstats-bukkit:1.3'
    testCompile 'junit:junit:4.11'
    testCompile 'org.hamcrest:hamcrest-library:1.2.1'
}

processResources {
    from (sourceSets.main.resources.srcDirs) {
        expand project.properties
        include 'plugin.yml'
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

shadowJar {
    dependencies {
        include(dependency(':worldguard-core'))
        relocate ("org.bstats", "com.sk89q.worldguard.bukkit.bstats") {
            include(dependency("org.bstats.bStats-Metrics:bstats-bukkit:1.3"))
        }
    }

    relocate('org.flywaydb', 'com.sk89q.worldguard.internal.flywaydb')
    relocate('com.sk89q.squirrelid', 'com.sk89q.worldguard.util.profile')
    relocate('org.json.simple', 'com.sk89q.worldguard.util.jsonsimple')

}

build.dependsOn(shadowJar)
